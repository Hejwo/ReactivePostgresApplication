buildscript {
	ext {
		springBootVersion = '2.0.9.BUILD-SNAPSHOT'
	}
	repositories {
		mavenCentral()
		maven { url 'https://repo.spring.io/snapshot' }
		maven { url 'https://repo.spring.io/milestone' }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'org.hejwo.r2dbc'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/snapshot' }
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/libs-milestone' }
}

springBoot {
	mainClassName = "org.hejwo.r2dbc.reactivepostgres.ReactivePostgresApplication"
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
	integrationTest {
		groovy {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integrationTest/groovy')
		}
		resources.srcDir file('src/integrationTest/resources')
	}
}

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-webflux'
	compileOnly 'org.projectlombok:lombok'

	// R2DBC
	compile group: 'org.springframework.data', name: 'spring-data-r2dbc', version: '1.0.0.M1'
	compile group: 'io.r2dbc', name: 'r2dbc-postgresql', version: '1.0.0.M6'
	
	// Database
	compile 'ru.yandex.qatools.embed:postgresql-embedded:1.21'
	compile 'org.flywaydb:flyway-core:4.0.2'
}

dependencies {
	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'io.projectreactor:reactor-test'

	testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4'
	testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
	testCompile 'org.codehaus.groovy:groovy:2.4.7'
}

task integrationTest(type: Test) {
	group 'verification'
	classpath = sourceSets.integrationTest.runtimeClasspath
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test